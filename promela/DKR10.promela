/* Dolev, Klawe & Rodeh for leader election in unidirectional ring
 * `An O(n log n) unidirectional distributed algorithm for extrema
 * finding in a circle,'  J. of Algs, Vol 3. (1982), pp. 245-260
 */

#define N	10	/* nr of processes (use 5 for demos) */
#define L	20	/* size of buffer  (>= 2*N) */

mtype = { one, two, winner };
chan q0 = [L] of {mtype, byte};
chan q1 = [L] of {mtype, byte};
chan q2 = [L] of {mtype, byte};
chan q3 = [L] of {mtype, byte};
chan q4 = [L] of {mtype, byte};
chan q5 = [L] of {mtype, byte};
chan q6 = [L] of {mtype, byte};
chan q7 = [L] of {mtype, byte};
chan q8 = [L] of {mtype, byte};
chan q9 = [L] of {mtype, byte};

byte nr_leaders = 0;

proctype node (chan inn, out; byte mynumber)
{	bit Active = 1, know_winner = 0;
	byte nr, maximum = mynumber, neighbourR;

	xr inn;
	xs out;

	printf("MSC: %d\n", mynumber);
	out!one(mynumber);
end:	do
	:: inn?one(nr) ->
		if
		:: Active -> 
			progress: if
			:: nr != maximum ->
				out!two(nr);
				neighbourR = nr
			:: else ->
				/* Raynal p.39:  max is greatest number */
				assert(nr == N);
				know_winner = 1;
				out!winner,nr;
			fi
		:: else ->
			out!one(nr)
		fi

	:: inn?two(nr) ->
		if
		:: Active -> 
			progress2: if
			:: neighbourR > nr && neighbourR > maximum ->
				maximum = neighbourR;
				out!one(neighbourR)
			:: else ->
				Active = 0
			fi
		:: else ->
			out!two(nr)
		fi
	:: inn?winner,nr ->
		if
		:: nr != mynumber ->
			printf("MSC: LOST\n");
		:: else ->
			printf("MSC: LEADER\n");
			nr_leaders++;
			assert(nr_leaders == 1)
		fi;
		if
		:: know_winner
		:: else -> out!winner,nr
		fi;
		break
	od
}

init {
	byte proc;
	atomic {
                run node (q0,q1,24); proc++;
                run node (q1,q2,14); proc++;
                run node (q2,q3,6); proc++;
                run node (q3,q4,0); proc++;
                run node (q4,q5,23); proc++;
                run node (q5,q6,21); proc++;
                run node (q6,q7,1); proc++;
                run node (q7,q8,3); proc++;
                run node (q8,q9,10); proc++;
                run node (q9,q0,22); proc++;
	}
}
